apply plugin: 'com.android.application'

allprojects {
    tasks.withType(JavaCompile).tap {
        configureEach {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

android {
    compileSdk 34

    defaultConfig {
        applicationId "mobile.android.pass"
        minSdkVersion 30
        targetSdk 34
        versionCode 16
        versionName "0.2.3"
    }
    buildTypes {
        debug {
            debuggable true
            manifestPlaceholders.usesCleartextTraffic = "true"
        }
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders.usesCleartextTraffic = "false"
        }
    }
    namespace 'mobile.android.pass'
    lint {
        abortOnError false
        checkReleaseBuilds false
    }
    buildFeatures {
        buildConfig true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Android backwards/forwards compatibility
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.window:window:1.2.0'

    // Android official http lib
    implementation 'com.android.volley:volley:1.2.1'

    // Working Bouncy Castle project for Android
    implementation 'com.madgag.spongycastle:pg:1.54.0.0'

    // TOTP/HOTP (2FA) simplified
    implementation 'com.eatthepath:java-otp:0.4.0'
    // It requires however to do our own String -> Key conversion since that is its input.
    // https://mvnrepository.com/artifact/commons-codec/commons-codec
    implementation 'commons-codec:commons-codec:1.16.1'
}
